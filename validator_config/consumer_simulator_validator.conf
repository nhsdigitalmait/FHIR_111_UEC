# emergency booking response validator ruleset used by tkw autotest
# NHS Interoperability Toolkit
#
#
#
# *****************************************************************************************

VALIDATION-RULESET-NAME FHIR REST Care Connect Emergency booking validations
VALIDATION-RULESET-VERSION 0.1
VALIDATION-RULESET-TIMESTAMP 20201016
VALIDATION-RULESET-AUTHOR Simon Farrow

#==========================================================================================

VALIDATE urn:nhs:names:services:careconnect:fhir:rest:read:metadata
#(remember to use 'VALIDATE-AS: urn:nhs:names:services:careconnect:fhir:rest:read:metadata with the messages)
# Now for DSTU3

CHECK hapifhirvalidator
ANNOTATION HapiFhir Profile validation

## response success 
#Schema Check on whole message
#CHECK schema TKW_ROOT/contrib/DMS_Schema/FGM/Schemas/bundle.xsd
#ANNOTATION Schema check (incorrect schema!)

#Transform Checks
CHECK xslt TKW_ROOT/contrib/Common/xslt/blank_attribute_checker.xslt ERROR
ANNOTATION "WARNING ONLY - test shows as fail if XML contains blank attributes"

CHECK xslt TKW_ROOT/contrib/Common/xslt/IllegalCharacters.xslt ERROR
ANNOTATION "WS-STD-01: Toolkit Messages MUST use UTF-8 encoding. Illegal character check may flag up some valid unicode characters, as it is not an exhaustive check. It will locate the use of non-rendering unicode control characters within the payload, sometimes erroneously introduced."

IF xpathexists /fhir:CapabilityStatement
THEN
	CHECK sub capability
END IF

#==========================================================================================
#

VALIDATE urn:nhs:names:services:careconnect:fhir:rest:search:slot
#(remember to use 'VALIDATE-AS: urn:nhs:names:services:gpconnect:fhir:rest:search:slot with the messages)

CHECK hapifhirvalidator
ANNOTATION HapiFhir Profile validation

IF xpathexists /fhir:Bundle
THEN
	CHECK sub slots
ELSE
	CHECK content xpathexists /fhir:OperationOutcome
	ANNOTATION Search slots fail check
END IF

#==========================================================================================
# Appointment Retrieve

VALIDATE urn:nhs:names:services:careconnect:fhir:rest:read:appointment

CHECK hapifhirvalidator
ANNOTATION HapiFhir Profile validation

IF xpathexists /fhir:Appointment
THEN
	CHECK sub appointment
ELSE
	CHECK content xpathexists /fhir:OperationOutcome
	ANNOTATION Retrieve appointment fail check
END IF

#==============================================================================
# Appointment Book

VALIDATE urn:nhs:names:services:careconnect:fhir:rest:create:appointment

CHECK hapifhirvalidator
ANNOTATION HapiFhir Profile validation

IF xpathexists /fhir:Appointment
THEN
	CHECK sub appointment
ELSE
	CHECK content xpathexists /fhir:OperationOutcome
	ANNOTATION Retrieve appointment fail check
END IF

#==============================================================================
# Appointment Cancel

VALIDATE urn:nhs:names:services:careconnect:fhir:rest:update:appointment

CHECK hapifhirvalidator
ANNOTATION HapiFhir Profile validation

IF xpathexists /fhir:Appointment
THEN
	CHECK sub appointment
ELSE
	CHECK content xpathexists /fhir:OperationOutcome
	ANNOTATION Cancel appointment fail check
END IF

#==============================================================================
#  validation of SFFS response 
SUBSET slots

	# removed because this has to be a dynamic check other wise we see a fail when we do SFFS on a known timeframe having no slots
	#CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Slot
	#ANNOTATION Get slots success check

	# schedules unique
	CHECK xpathequals boolean(count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Schedule) = count(distinct-values(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Schedule/fhir:id/@value))) true
	ANNOTATION All schedule ids must be unique (SCAL Requirement P-CRT-RES-MORE-SLOT-ONCE-BUND)
	
	# health care services unique
	CHECK xpathequals boolean(count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:HealthCareService) = count(distinct-values(/fhir:Bundle/fhir:entry/fhir:resource/fhir:HealthCareService/fhir:id/@value))) true
	ANNOTATION All HealthCareService ids must be unique (SCAL Requirement P-CRT-RES-MORE-SLOT-ONCE-BUND)
	
	# practitioners unique
	CHECK xpathequals boolean(count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Practitioner) = count(distinct-values(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Practitioner/fhir:id/@value))) true
	ANNOTATION All Practitioner ids must be unique (SCAL Requirement P-CRT-RES-MORE-SLOT-ONCE-BUND)
	
	# practitioner roles unique
	CHECK xpathequals boolean(count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:PractitionerRole) = count(distinct-values(/fhir:Bundle/fhir:entry/fhir:resource/fhir:PractitionerRole/fhir:id/@value))) true
	ANNOTATION All PractitionerRole ids must be unique (SCAL Requirement P-CRT-RES-MORE-SLOT-ONCE-BUND)

	# locations unique
	CHECK xpathequals boolean(count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Location) = count(distinct-values(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Location/fhir:id/@value))) true
	ANNOTATION All Location ids must be unique (SCAL Requirement P-CRT-RES-MORE-SLOT-ONCE-BUND)

	# slots profile check
	CHECK xpathequals boolean(count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Slot/fhir:meta/fhir:profile/@value['https://fhir.hl7.org.uk/STU3/StructureDefinition/CareConnect-Slot-1']) = count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Slot)) true
	ANNOTATION All slot records should have a profile value of https://fhir.hl7.org.uk/STU3/StructureDefinition/CareConnect-Slot-1 (SCAL Requirement P-CRT-RES-RET-SLOT-META-PROF)
	
	# slots schedule actor
	CHECK xpathequals boolean(count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Schedule/fhir:actor/fhir:reference/@value[starts-with(.,'HealthcareService/')]) = count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Schedule)) true
	ANNOTATION Each schedule response in the response must have an actor referencing HealthcareService (SCAL Requirement P-CRT-RES-PRACTROLE-REQ-SLOT-INC-BUND)
	
	# slots valid schedule reference
	CHECK xpathequals boolean(count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Slot/fhir:schedule/fhir:reference/@value[not(starts-with(.,'Schedule/'))]) = 0) true
	ANNOTATION All slot records should have valid schedule references valid (SCAL Requirement P-CRT-RES-ACT-REQ-SLOT-INC-BUND)

	# slots start time exists
	# TODO This needs beefing up to check the format
	CHECK xpathequals boolean(count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Slot/fhir:start/@value) = count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Slot)) true
	ANNOTATION All slot records should have a start timestamp (SCAL Requirement P-CRT-RES-RET-SLOT-START-TIMESTAMP-UTC-OFFSET)
	
	# slots end time exists
	# TODO This needs beefing up to check the format
	CHECK xpathequals boolean(count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Slot/fhir:end/@value) = count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Slot)) true
	ANNOTATION All slot records should have an end timestamp (SCAL Requirement P-CRT-RES-RET-SLOT-END-TIMESTAMP-UTC-OFFSET)

	# slot end dates must be later than start dates
	CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Slot/fhir:start/@value[ . > ../../fhir:end/@value]) 0
	ANNOTATION No slot should have an end date preceeding the start date

	# EB Constraint slot identifier 1..1
	CHECK xpathequals boolean(count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Slot[count(fhir:identifier)=1]) = count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Slot)) true
	ANNOTATION Emergency Booking Constraint All Slots must have exactly one identifier
	
	# EB Constraint Schedule actor count 1..3
	CHECK xpathequals boolean(count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Schedule[count(fhir:actor)>=1 and count(fhir:actor)<=3]) = count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Schedule)) true
	ANNOTATION Emergency Booking Constraint All schedules must have 1..3 actors

	# EB Constraint slots schedule Healthcare actor must be 1
	CHECK xpathequals boolean(count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Schedule[count(fhir:actor/fhir:reference/@value[starts-with(.,'HealthcareService/')])=1]) = count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Schedule)) true
	ANNOTATION Emergency Booking Constraint Each schedule in the response must have exactly one actor referencing HealthcareService

	# EB Constraint slots schedule Practitioner actor must be 0..1
	CHECK xpathequals boolean(count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Schedule[count(fhir:actor/fhir:reference/@value[starts-with(.,'Practitioner/')])<=1]) = count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Schedule)) true
	ANNOTATION Emergency Booking Constraint Each schedule in the response must have 0..1 actors referencing Practitioner
	
	# EB Constraint slots schedule PractitionerRole actor must be 0..1
	CHECK xpathequals boolean(count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Schedule[count(fhir:actor/fhir:reference/@value[starts-with(.,'PractitionerRole/')])<=1]) = count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Schedule)) true
	ANNOTATION Emergency Booking Constraint Each schedule in the response must have 0..1 actors referencing PractitionerRole

	# EB Constraint slots HealthcareService Location reference must be 0..1
	CHECK xpathequals boolean(count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:HealthcareService[count(fhir:location/fhir:reference/@value[starts-with(.,'Location/')])<=1]) = count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:HealthcareService)) true
	ANNOTATION Emergency Booking Constraint Each HealthcareService in the response must have 0..1 elements referencing Location
	#
	# EB Constraint slots HealthcareService identifier must be present
	CHECK xpathequals boolean(count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:HealthcareService[fhir:identifier]) = count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:HealthcareService)) true
	ANNOTATION Emergency Booking Constraint Each HealthcareService in the response must have 1 identifier element


#==============================================================================
#

SUBSET appointment
	CHECK xpathexists /fhir:Appointment
	ANNOTATION Get appointment success check

	# C39 appointment profile check - handled by profiles?
	CHECK xpathequals /fhir:Appointment/fhir:meta/fhir:profile/@value https://fhir.hl7.org.uk/STU3/StructureDefinition/CareConnect-Appointment-1
	ANNOTATION All Appointment records should have a profile value of https://fhir.hl7.org.uk/STU3/StructureDefinition/CareConnect-Appointment-1 (SCAL Requirement C-POST-INC-APP-RES)
	
	# C41 appointment resource declared language must be en-GB
	CHECK xpathequals /fhir:Appointment/fhir:language/@value en-GB
	ANNOTATION All Appointment records should have a declared language of en-GB (SCAL Requirement C-APP-RES-LANG-ENG-GB)
	
	# C43 Appointment references a single slot
	CHECK xpathequals count(/fhir:Appointment/fhir:slot/fhir:reference) 1
	ANNOTATION All Appointment records should reference a single slot (SCAL Requirement C-APP-RES-REFS-ONE-SLOT)

	# C44 Appointment slot reference is correctly formed
	CHECK xpathmatches /fhir:Appointment/fhir:slot/fhir:reference/@value ^#.*$
	ANNOTATION All Appointment slot references should have the correct form (SCAL Requirement C-APP-RESS-SLOT-REF-CONTD-RES)

	CHECK xpathcompare substring(/fhir:Appointment/fhir:slot/fhir:reference/@value,2) /fhir:Appointment/fhir:contained/fhir:Slot/fhir:id/@value
	ANNOTATION All Appointment slot references reference a valid slot (SCAL Requirement C-APP-RESS-SLOT-REF-CONTD-RES)

	# C45 Appointment created date must be of the form yyyy-mm-ddThh:MM:SS.sssZ SCAL C45 SCAL says yyyy-mm-ddThh:MM:SS.sssZ
	# This format is as per fhir instant
	CHECK xpathmatches /fhir:Appointment/fhir:created/@value ^([0-9]([0-9]([0-9][1-9]|[1-9]0)|[1-9]00)|[1-9]000)-(0[1-9]|1[0-2])-(0[1-9]|[1-2][0-9]|3[0-1])T([01][0-9]|2[0-3]):[0-5][0-9]:([0-5][0-9]|60)(\.[0-9]+)?(Z|(\+|-)((0[0-9]|1[0-3]):[0-5][0-9]|14:00))$
	ANNOTATION All Appointment created dates should have the correct form (SCAL Requirement C-APP-RES-APP-CRT-CUR-TIMESTAMP)
	
	# C46 Appointment has exactly one participant:actor resource with a reference pointing to a contained resource
	CHECK xpathmatches count(/fhir:Appointment/fhir:participant/fhir:actor/fhir:reference/@value) ^1$
	ANNOTATION Appointment has exactly one participant:actor resource (SCAL Requirement C-APP-RES-APP-ONE-ACT-RES-REF-CONTD-RES)

	SET literal $actor /fhir:Appointment/fhir:participant/fhir:actor
	CHECK xpathmatches $actor/fhir:reference/@value ^#.*$
	ANNOTATION Appointment has exactly one participant:actor resource with a reference pointing to a contained resource (SCAL Requirement C-APP-RES-APP-ONE-ACT-RES-REF-CONTD-RES)

	# C47 Participant:actor resource has one identifier marked as use: official, with system: "https://fhir.nhs.uk/Id/nhs-number" and value of a valid NHS Number.
	# and C54 
	CHECK xpathmatches $actor/fhir:identifier/fhir:use/@value ^official$
	ANNOTATION Participant:actor resource has one identifier marked as use: official. (SCAL Requirement C-ACT-RES-CON-SYS-NHS-NO-ACT-RES-ONE-IDENT-USE)

	CHECK xpathmatches $actor/fhir:identifier/fhir:system/@value ^https://fhir.nhs.uk/Id/nhs-number$
	ANNOTATION Participant:actor resource has identifier with system: "https://fhir.nhs.uk/Id/nhs-number". (SCAL Requirement C-ACT-RES-CON-SYS-NHS-NO-ACT-RES-ONE-IDENT-USE)

	CHECK xpathmatches $actor/fhir:identifier/fhir:value/@value ^[0-9]{10}$
	ANNOTATION Participant:actor resource has identifier with value of a valid NHS Number.(SCAL Requirement C-PATIENT-RES-CONS-SYS-NHS-NO-ONE-IDENT-OFFICIAL)

	# C48 Participant:actor resource has a display value set to the Patient's full name in a human readable format. NB We are just checking that its there no more
	CHECK xpathmatches count(/fhir:Appointment/fhir:participant/fhir:actor/fhir:display/@value) ^1$
	ANNOTATION Participant:actor resource has a display value set to the Patient's full name in a human readable format. (SCAL Requirement C-ACT-RES-DPLAY-VALUE-FULL-NAME-READ-FORMAT)
	
	# C49 If the Consumer will also send a CDA document, the Appointment has exactly one supportingInformation reference to a contained resource i.e. prefixed with # (this will not be a full FHIR reference simply a string value prefixed with # ). 
	CHECK xpathmatches count(/fhir:Appointment/fhir:supportingInformation/fhir:reference/@value) ^(0|1)$
	ANNOTATION If the Consumer will also send a CDA document, the Appointment has exactly one supportingInformation reference to a contained resource. (SCAL Requirement C-REFAL-REF-CONS-CDA-DOC-APP-ONE-SUPP-INFO-REF-CONTD-RES)

	SET literal $supportingInformation /fhir:Appointment/fhir:supportingInformation
	IF xpathexists $supportingInformation/fhir:reference/@value
	THEN
		CHECK xpathmatches $supportingInformation/fhir:reference/@value ^#.*$
		ANNOTATION the Appointment has exactly one supportingInformation reference to a contained resource i.e. prefixed with # (this will not be a full FHIR reference simply a string value prefixed with # ). (SCAL Requirement C-REFAL-REF-CONS-CDA-DOC-APP-ONE-SUPP-INFO-REF-CONTD-RES)
	ENDIF

	# C50 The Appointment resource has exactly one contained Patient resource, with a local identifier (valid only within the containing resource). 
	# and C53 (POST) The text here is referring to the id element
	CHECK xpathmatches count(/fhir:Appointment/fhir:contained/fhir:Patient) ^1$
	ANNOTATION The Appointment resource has exactly one contained Patient resource, with a local identifier (SCAL Requirement C-PATIENT-RES-ONE-CONTD)

	# The appointment resource contains exactly one DocumentReference
	CHECK xpathmatches count(/fhir:Appointment/fhir:contained/fhir:DocumentReference) ^1$
	ANNOTATION The Appointment resource has exactly one contained DocumentReference resource (Emergency Booking overrides)

	# The appointment resource contains exactly one Slot
	CHECK xpathmatches count(/fhir:Appointment/fhir:contained/fhir:Slot) ^1$
	ANNOTATION The Appointment resource has exactly one contained Slot resource (iEmeregebcy Booking override)

	SET literal $Patient /fhir:Appointment/fhir:contained/fhir:Patient

	CHECK xpathexists $Patient/fhir:id/@value
	ANNOTATION The Appointment resource has exactly one contained Patient resource, with a local identifier (SCAL Requirement C-PATIENT-RES-ONE-CONTD)

	CHECK xpathmatches boolean(count(/fhir:Appointment/fhir:contained/fhir:Patient/fhir:identifier)<=1) true
	ANNOTATION Emergency Booking constraint The Appointment Patient resource has 0..1 identifier elements

	# identifier is only present if the provider has an nhs number for the patient
	IF xpathmatches count(/fhir:Appointment/fhir:contained/fhir:Patient/fhir:identifier) ^1$ 
	THEN
		# TODO C-POST-INC-APP-RES-NHS-NO-CONDT-PATIENT-RES-ONE-IDENT states that there will be exactly one patient identifer but only if the patient has an nhs number
		# C-POST-INC-APP-RES-NOT-NHS-NO-CONDT-PATIENT-RES-NOT-IDENT If the consumer system does not have an nhs number there should be no identifier
		CHECK xpathmatches count(/fhir:Appointment/fhir:contained/fhir:Patient/fhir:identifier) ^1$
		# DN what does this mean exactly?
		ANNOTATION The Appointment resource has exactly one contained Patient resource, with a local identifier (valid only within the containing resource). (SCAL Requirement C-PATIENT-RES-ONE-CONTD)

		CHECK xpathmatches boolean(count(/fhir:Appointment/fhir:contained/fhir:Patient/fhir:identifier/fhir:use)<=1) true
		ANNOTATION Emergency Booking constraint The Appointment Patient identifier resource has 0..1 use element

		CHECK xpathmatches count(/fhir:Appointment/fhir:contained/fhir:Patient/fhir:identifier/fhir:system) ^1$
		ANNOTATION Emergency Booking constraint The Appointment Patient identifier resource has exactly one system element

		CHECK xpathmatches count(/fhir:Appointment/fhir:contained/fhir:Patient/fhir:identifier/fhir:value) ^1$
		ANNOTATION Emergency Booking constraint The Appointment Patient identifier resource has exactly one value element
	
		# C55 The contained Patient resource must include the extension "https://fhir.hl7.org.uk/STU3/StructureDefinition/Extension-CareConnect-NHSNumberVerificationStatus-1" stating that the supplied NHs Number is present and verified. 
		CHECK xpathequals boolean(/fhir:Appointment/fhir:contained/fhir:Patient/fhir:identifier/fhir:extension[@url='https://fhir.hl7.org.uk/STU3/StructureDefinition/Extension-CareConnect-NHSNumberVerificationStatus-1']/fhir:valueCodeableConcept/fhir:coding[fhir:system/@value='https://fhir.hl7.org.uk/STU3/CodeSystem/CareConnect-NHSNumberVerificationStatus-1']/fhir:display/@value = 'Number present and verified') true
		ANNOTATION The contained Patient resource must include the extension "https://fhir.hl7.org.uk/STU3/StructureDefinition/Extension-CareConnect-NHSNumberVerificationStatus-1" stating that the supplied NHs Number is present and verified. (SCAL Requirement C-PATIENT-RES-CONS-SYS-NHS-NO-INC-EXT)

	ENDIF

	# C51 The contained Patient resource's id matches the Participant:actor resource reference (without the # ). 
	CHECK xpathequals boolean(/fhir:Appointment/fhir:contained/fhir:Patient/fhir:id/@value = substring(/fhir:Appointment/fhir:participant/fhir:actor/fhir:reference/@value,2)) true
	ANNOTATION The contained Patient resource's id matches the Participant:actor resource reference (without the # ). (SCAL Requirement C-PATIENT-RES-ID-MATCH-ACT-RES-REF)

	# C52 The contained Patient resource must claim in Meta section to conform to https://fhir.hl7.org.uk/STU3/StructureDefinition/CareConnect-Patient-1 
	CHECK xpathequals boolean(/fhir:Appointment/fhir:contained/fhir:Patient/fhir:meta/fhir:profile/@value = 'https://fhir.hl7.org.uk/STU3/StructureDefinition/CareConnect-Patient-1') true
	ANNOTATION The contained Patient resource must claim in Meta section to conform to https://fhir.hl7.org.uk/STU3/StructureDefinition/CareConnect-Patient-1. (SCAL Requirement C-PATIENT-RES-CLAIM-META-SECT)

	
	# C56 The contained Patient resource should have exactly one Name. 
	CHECK xpathequals count(/fhir:Appointment/fhir:contained/fhir:Patient/fhir:name) 1
	ANNOTATION The contained Patient resource should have exactly one Name. (SCAL Requirement C-PATIENT-RES-ONE-NAME)
	
	# C57 The contained Patient resource must have a name with use set to official, and a family and given name values not set to null. 
	CHECK xpathequals $Patient/fhir:name/fhir:use/@value official
	ANNOTATION The contained Patient resource must have a name with use set to official. (SCAL Requirement C-PATIENT-RES-NAME-OFFI-FAMI-GIVE-NAME-NOT-NULL)

	CHECK xpathexists $Patient/fhir:name/fhir:family/@value 
	ANNOTATION The contained Patient resource must have a name with family values not set to null. (SCAL Requirement C-PATIENT-RES-NAME-OFFI-FAMI-GIVE-NAME-NOT-NULL)

	CHECK xpathexists $Patient/fhir:name/fhir:given/@value 
	ANNOTATION The contained Patient resource must have a name with given name values not set to null. (SCAL Requirement C-PATIENT-RES-NAME-OFFI-FAMI-GIVE-NAME-NOT-NULL)

	# C58 The contained Patient resource should have a ContactPoint set, with a system of Phone, and a value of a valid UK phone number. 
	# define valid uk phone number are spaces allowed?
	CHECK xpathmatches $Patient/fhir:telecom[fhir:system/@value='phone']/fhir:value/@value ^[0-9 ]+$
	ANNOTATION The contained Patient resource should have a ContactPoint set, with a system of Phone, and a value of a valid UK phone number. (SCAL Requirement C-PATIENT-RES-CONT-POINT-SYS-PHONE-VALID-UK)

	# C59 The contained Patient resource may have a ContactPoint set, with a system of email, if so the format of the value must be valid for an email address..
	IF xpathexists $Patient/fhir:telecom[fhir:system/@value='email'] 
	THEN
		# define valid email address
		CHECK xpathmatches $Patient/fhir:telecom[fhir:system/@value='email']/fhir:value/@value ^.+@.+$$
		ANNOTATION The contained Patient resource may have a ContactPoint set, with a system of email, if so the format of the value must be valid for an email address. (SCAL Requirement C-PATIENT-RES-CONT-POINT-SYS-EMAIL-VALID)
	ENDIF
	
	# C60 The contained Patient resource must have a Gender value.
	CHECK xpathexists $Patient/fhir:gender/@value
	ANNOTATION The contained Patient resource must have a Gender value. (SCAL Requirement C-PATIENT-RES-GENDER-VALUE)
	
	# C61 The contained Patient resource must have a date of birth value.
	CHECK xpathexists $Patient/fhir:birthDate/@value
	ANNOTATION The contained Patient resource must have a date of birth value. (SCAL Requirement C-PATIENT-RES-DOB-VALUE)
	
	# C62 The contained Patient resource must have at least one address.
	CHECK xpathequals boolean(count(/fhir:Appointment/fhir:contained/fhir:Patient/fhir:address) > 0) true
	ANNOTATION The contained Patient resource must have at least one address.. (SCAL Requirement C-PATIENT-RES-ONE-ADDRESS)
	
	# C63 The contained Patient resource must have exactly one address with use set to home..
	CHECK xpathequals count(/fhir:Appointment/fhir:contained/fhir:Patient/fhir:address[fhir:use/@value='home']) 1
	ANNOTATION The contained Patient resource must have exactly one address with use set to home. (SCAL Requirement C-PATIENT-RES-ONE-ADDRESS-SET-HOME)
	
	# C64 The home address of the contained Patient resource must have a valid postcode.
	# well formed or actually valid????
	# see https://stackoverflow.com/questions/164979/regex-for-matching-uk-postcodes
	CHECK xpathmatches $Patient/fhir:address/fhir:postalCode/@value ^([Gg][Ii][Rr] 0[Aa]{2})|((([A-Za-z][0-9]{1,2})|(([A-Za-z][A-Ha-hJ-Yj-y][0-9]{1,2})|(([A-Za-z][0-9][A-Za-z])|([A-Za-z][A-Ha-hJ-Yj-y][0-9][A-Za-z]?))))\s?[0-9][A-Za-z]{2})$
	ANNOTATION The home address of the contained Patient resource must have a valid postcode. (SCAL Requirement C-PATIENT-RES-HOME-ADDRESS-VALID-PCODE)
	
	# C64 The contained Patient resource must have exactly one address with use set to home.
	CHECK xpathequals count(/fhir:Appointment/fhir:contained/fhir:Patient/fhir:address[fhir:use/@value='home']) 1
	ANNOTATION The contained Patient resource must have exactly one address with use set to home. (SCAL Requirement C-PATIENT-RES-HOME-ADDRESS-VALID-PCODE)

	SET literal $DocumentReference /fhir:Appointment/fhir:contained/fhir:DocumentReference
	IF xpathexists /fhir:Appointment/fhir:supportingInformation/fhir:reference/@value 
	THEN
		# C65 If the Consumer will also send a CDA document, the Appointment has exactly one contained DocumentReference resource.
		CHECK xpathequals count(/fhir:Appointment/fhir:contained/fhir:DocumentReference) 1
		ANNOTATION If the Consumer will also send a CDA document, the Appointment has exactly one contained DocumentReference resource. (SCAL Requirement C-DOCREF-RES-CDA-APP-ONE-DOCREF-RES)
		
		# C66 If the consumer will also send a CDA document, the contained DocumentReference resource must have an id which matches the supportingInformation reference (without the # ).
		CHECK xpathequals boolean(/fhir:Appointment/fhir:contained/fhir:DocumentReference/fhir:id/@value = substring(/fhir:Appointment/fhir:supportingInformation/fhir:reference/@value,2)) true
		ANNOTATION If the consumer will also send a CDA document, the contained DocumentReference resource must have an id which matches the supportingInformation reference (without the # ). (SCAL Requirement C-DOCREF-RES-ID-MATCH-INFO-REF)
		
		# C67 The contained DocumentReference resource must have an identifier of system "uuid" and value being the /root/id value from the associated CDA document.
		CHECK xpathequals $DocumentReference/fhir:identifier/fhir:system/@value https://tools.ietf.org/html/rfc4122
		ANNOTATION The contained DocumentReference resource must have an identifier of system "uuid" and value being the /root/id value from the associated CDA document. (SCAL Requirement C-DOCREF-RES-IDENT-SYS-VALUE-ROOT-ID-CDA-DOC)
		
		# C68 The contained DocumentReference resource must have a status of "current".
		CHECK xpathequals $DocumentReference/fhir:status/@value current
		ANNOTATION The contained DocumentReference resource must have a status of "current". (SCAL Requirement C-DOCREF-RES-STAT-CUR)
	
		# C69 The contained DocumentReference resource must have a type of coding with values:
		# system: urn:oid:2.16.840.1.113883.2.1.3.2.4.18.17
		CHECK xpathequals $DocumentReference/fhir:type/fhir:coding/fhir:system/@value urn:oid:2.16.840.1.113883.2.1.3.2.4.18.17
		ANNOTATION The contained DocumentReference resource must have a coding system value of "urn:oid:2.16.840.1.113883.2.1.3.2.4.18.17". (SCAL Requirement C-DOCREF-RES-TYPE-SYS-CODE-DPLAY)

		# code: POCD_MT200001GB02
		CHECK xpathequals $DocumentReference/fhir:type/fhir:coding/fhir:code/@value POCD_MT200001GB02
		ANNOTATION The contained DocumentReference resource must have a coding code value of "POCD_MT200001GB02". (SCAL Requirement C-DOCREF-RES-TYPE-SYS-CODE-DPLAY)

		# display: Integrated Urgent Care Report
		CHECK xpathequals $DocumentReference/fhir:type/fhir:coding/fhir:display/@value Integrated Urgent Care Report
		ANNOTATION The contained DocumentReference resource must have a coding display value of "Integrated Urgent Care Report". (SCAL Requirement C-DOCREF-RES-TYPE-SYS-CODE-DPLAY)
		
		# C70 The contained DocumentReference resource must have a indexed value of the current time. TODO define current
		# This just checks for a valid format as per fhir instant see https://www.hl7.org/fhir/datatypes.html#instant
		CHECK xpathmatches $DocumentReference/fhir:indexed/@value ^([0-9]([0-9]([0-9][1-9]|[1-9]0)|[1-9]00)|[1-9]000)-(0[1-9]|1[0-2])-(0[1-9]|[1-2][0-9]|3[0-1])T([01][0-9]|2[0-3]):[0-5][0-9]:([0-5][0-9]|60)(\.[0-9]+)?(Z|(\+|-)((0[0-9]|1[0-3]):[0-5][0-9]|14:00))$
		ANNOTATION The contained DocumentReference resource must have a indexed value of the current time. (SCAL Requirement C-DOCREF-RES-INDEX-VALUE-CUR-TIME)
	
		# C71 The contained DocumentReference resource must have a 'content' of type 'attachment' with values:
		# contentType: application/hl7-v3+xml
		CHECK xpathequals $DocumentReference/fhir:content/fhir:attachment/fhir:contentType/@value application/hl7-v3+xml
		ANNOTATION The contained DocumentReference resource must have a 'content' of type 'attachment' with contentType value application/hl7-v3+xml (SCAL Requirement C-DOCREF-RES-CONTENT-ATTACH-VALUES)

		# contentType: language: en
		CHECK xpathequals $DocumentReference/fhir:content/fhir:attachment/fhir:language/@value en
		ANNOTATION The contained DocumentReference resource must have a 'content' of type 'attachment' with language value en. (SCAL Requirement C-DOCREF-RES-CONTENT-ATTACH-VALUES)
	ENDIF

	# see https://dev-non-live.uec-appts.docs.opennhs.io/fs_fhir_overrides.html
	#
	# EB Constraint Appointment must have exactly one start element
	CHECK xpathequals count(/fhir:Appointment/fhir:start/@value) 1
	ANNOTATION Emergency Booking Constraint Each Appointment in the response must have exactly 1 start date

	# EB Constraint Appointment must have exactly one end element
	CHECK xpathequals count(/fhir:Appointment/fhir:end/@value) 1
	ANNOTATION Emergency Booking Constraint Each Appointment in the response must have exactly 1 end date
	#
	# EB Constraint Appointment must have exactly one created element
	CHECK xpathequals count(/fhir:Appointment/fhir:created/@value) 1
	ANNOTATION Emergency Booking Constraint Each Appointment in the response must have exactly 1 created date
	
	# EB Constraint Appointment must have exactly one description element
	CHECK xpathequals count(/fhir:Appointment/fhir:description/@value) 1
	ANNOTATION Emergency Booking Constraint Each Appointment in the response must have exactly 1 description element

	# EB Constraint Appointment must have exactly one slot element
	CHECK xpathequals count(/fhir:Appointment/fhir:slot/fhir:reference/@value) 1
	ANNOTATION Emergency Booking Constraint Each Appointment in the response must have exactly 1 slot reference element
	
	# EB Constraint Appointment must have exactly one supportingInformation element
	CHECK xpathequals count(/fhir:Appointment/fhir:supportingInformation/fhir:reference/@value) 1
	ANNOTATION Emergency Booking Constraint Each Appointment in the response must have exactly 1 supportingInformation reference element
	
	# EB Constraint Appointment must have 0..1 incomingReferral elements
	CHECK xpathequals boolean(count(/fhir:Appointment/fhir:incomingReferral <= 1 )) true
	ANNOTATION Emergency Booking Constraint Each Appointment in the response must have 0..1 incomingReferral elements

	# EB Constraint Appointment Patient must have exactly one id element
	CHECK xpathequals count(/fhir:Appointment/fhir:contained/fhir:Patient/fhir:id) 1
	ANNOTATION Emergency Booking Constraint Each Appointment Patient in the response must have exactly one id element
	#
	# EB Constraint Appointment Patient must have 0..1 identifier elements
	CHECK xpathequals boolean(count(/fhir:Appointment/fhir:contained/fhir:Patient/fhir:identifier) <=1) true
	ANNOTATION Emergency Booking Constraint Each Appointment Patient in the response must have 0..1 identifier elements

#==============================================================================
#

SUBSET capability
	CHECK xpathexists /fhir:CapabilityStatement/fhir:publisher
	ANNOTATION Get metadata publisher check

	CHECK xpathexists /fhir:CapabilityStatement/fhir:fhirVersion
	ANNOTATION Get metadata fhirVersion check

	CHECK xpathexists /fhir:CapabilityStatement/fhir:format[@value='application/fhir+json']
	ANNOTATION Get metadata json format check

	CHECK xpathexists /fhir:CapabilityStatement/fhir:format[@value='application/fhir+xml']
	ANNOTATION Get metadata xml format check

	CHECK xpathequals count(/fhir:CapabilityStatement/fhir:profile) 10
	ANNOTATION Get metadata profile count check

	CHECK xpathequals count(/fhir:CapabilityStatement/fhir:profile)=count(distinct-values(/fhir:CapabilityStatement/fhir:profile/fhir:reference/@value)) true
	ANNOTATION Get metadata profile uniqueness check

	CHECK xpathmatches /fhir:CapabilityStatement/fhir:profile[1]/fhir:reference/@value  ^https://fhir.hl7.org.uk/STU3/StructureDefinition/CareConnect-(Slot|HealthcareService|Schedule|Appointment|Patient|DocumentReference|Practitioner|PractitionerRole|Organization|Location)-1$
	ANNOTATION Get metadata profile reference 1 is valid

	CHECK xpathmatches /fhir:CapabilityStatement/fhir:profile[2]/fhir:reference/@value  ^https://fhir.hl7.org.uk/STU3/StructureDefinition/CareConnect-(Slot|HealthcareService|Schedule|Appointment|Patient|DocumentReference|Practitioner|PractitionerRole|Organization|Location)-1$
	ANNOTATION Get metadata profile reference 2 is valid

	CHECK xpathmatches /fhir:CapabilityStatement/fhir:profile[3]/fhir:reference/@value  ^https://fhir.hl7.org.uk/STU3/StructureDefinition/CareConnect-(Slot|HealthcareService|Schedule|Appointment|Patient|DocumentReference|Practitioner|PractitionerRole|Organization|Location)-1$
	ANNOTATION Get metadata profile reference 3 is valid

	CHECK xpathmatches /fhir:CapabilityStatement/fhir:profile[4]/fhir:reference/@value  ^https://fhir.hl7.org.uk/STU3/StructureDefinition/CareConnect-(Slot|HealthcareService|Schedule|Appointment|Patient|DocumentReference|Practitioner|PractitionerRole|Organization|Location)-1$
	ANNOTATION Get metadata profile reference 4 is valid

	CHECK xpathmatches /fhir:CapabilityStatement/fhir:profile[5]/fhir:reference/@value  ^https://fhir.hl7.org.uk/STU3/StructureDefinition/CareConnect-(Slot|HealthcareService|Schedule|Appointment|Patient|DocumentReference|Practitioner|PractitionerRole|Organization|Location)-1$
	ANNOTATION Get metadata profile reference 5 is valid

	CHECK xpathmatches /fhir:CapabilityStatement/fhir:profile[6]/fhir:reference/@value  ^https://fhir.hl7.org.uk/STU3/StructureDefinition/CareConnect-(Slot|HealthcareService|Schedule|Appointment|Patient|DocumentReference|Practitioner|PractitionerRole|Organization|Location)-1$
	ANNOTATION Get metadata profile reference 6 is valid

	CHECK xpathmatches /fhir:CapabilityStatement/fhir:profile[7]/fhir:reference/@value  ^https://fhir.hl7.org.uk/STU3/StructureDefinition/CareConnect-(Slot|HealthcareService|Schedule|Appointment|Patient|DocumentReference|Practitioner|PractitionerRole|Organization|Location)-1$
	ANNOTATION Get metadata profile reference 7 is valid

	CHECK xpathmatches /fhir:CapabilityStatement/fhir:profile[8]/fhir:reference/@value  ^https://fhir.hl7.org.uk/STU3/StructureDefinition/CareConnect-(Slot|HealthcareService|Schedule|Appointment|Patient|DocumentReference|Practitioner|PractitionerRole|Organization|Location)-1$
	ANNOTATION Get metadata profile reference 8 is valid

	CHECK xpathmatches /fhir:CapabilityStatement/fhir:profile[9]/fhir:reference/@value  ^https://fhir.hl7.org.uk/STU3/StructureDefinition/CareConnect-(Slot|HealthcareService|Schedule|Appointment|Patient|DocumentReference|Practitioner|PractitionerRole|Organization|Location)-1$
	ANNOTATION Get metadata profile reference 9 is valid

	CHECK xpathmatches /fhir:CapabilityStatement/fhir:profile[10]/fhir:reference/@value  ^https://fhir.hl7.org.uk/STU3/StructureDefinition/CareConnect-(Slot|HealthcareService|Schedule|Appointment|Patient|DocumentReference|Practitioner|PractitionerRole|Organization|Location)-1$
	ANNOTATION Get metadata profile reference 10 is valid

	# Appointment Rest Resource
	CHECK xpathmatches count(/fhir:CapabilityStatement/fhir:rest/fhir:resource) ^2$
	ANNOTATION Get metadata rest resource count is 2

	CHECK xpathmatches count(/fhir:CapabilityStatement/fhir:rest/fhir:resource/fhir:type/@value)=count(distinct-values(/fhir:CapabilityStatement/fhir:rest/fhir:resource/fhir:type/@value))  ^true$
	ANNOTATION Get metadata rest appointment resources are unique

	SET literal $rest /fhir:CapabilityStatement/fhir:rest

	CHECK xpathmatches $rest/fhir:resource[1]/fhir:type/@value  ^Appointment|Slot$
	ANNOTATION Get metadata rest resource 1 is valid

	CHECK xpathmatches $rest/fhir:resource[2]/fhir:type/@value  ^Appointment|Slot$
	ANNOTATION Get metadata rest resource 2 is valid

	SET literal $Appointment /fhir:CapabilityStatement/fhir:rest/fhir:resource[fhir:type/@value='Appointment']

	CHECK xpathmatches count(/fhir:CapabilityStatement/fhir:rest/fhir:resource[fhir:type/@value='Appointment']/fhir:interaction/fhir:code)  ^3$
	ANNOTATION Get metadata rest appointment resource interaction count is valid

	CHECK xpathmatches count(/fhir:CapabilityStatement/fhir:rest/fhir:resource[fhir:type/@value='Appointment']/fhir:interaction/fhir:code/@value)=count(distinct-values(/fhir:CapabilityStatement/fhir:rest/fhir:resource[fhir:type/@value='Appointment']/fhir:interaction/fhir:code/@value))  ^true$
	ANNOTATION Get metadata rest appointment resource interactions are unique

	CHECK xpathmatches $Appointment/fhir:interaction[1]/fhir:code/@value ^read|create|search-type$
	ANNOTATION Get metadata rest appointment resource interaction 1 is valid

	CHECK xpathmatches $Appointment/fhir:interaction[2]/fhir:code/@value ^read|create|search-type$
	ANNOTATION Get metadata rest appointment resource interaction 2 is valid

	CHECK xpathmatches $Appointment/fhir:interaction[3]/fhir:code/@value ^read|create|search-type$
	ANNOTATION Get metadata rest appointment resource interaction 3 is valid

	CHECK xpathmatches count(/fhir:CapabilityStatement/fhir:rest/fhir:resource[fhir:type/@value='Appointment']/fhir:searchParam)  ^3$
	ANNOTATION Get metadata rest appointment resource searchParam count is valid

	CHECK xpathmatches count(/fhir:CapabilityStatement/fhir:rest/fhir:resource[fhir:type/@value='Appointment']/fhir:searchParam/fhir:name/@value)=count(distinct-values(/fhir:CapabilityStatement/fhir:rest/fhir:resource[fhir:type/@value='Appointment']/fhir:searchParam/fhir:name/@value))  ^true$
	ANNOTATION Get metadata rest appointment resource searchParams are unique

	CHECK xpathmatches $Appointment/fhir:searchParam[1]/fhir:name/@value ^identifier|appointmentId|version$
	ANNOTATION Get metadata rest appointment resource searchParam 1 is valid

	CHECK xpathmatches $Appointment/fhir:searchParam[2]/fhir:name/@value ^identifier|appointmentId|version$
	ANNOTATION Get metadata rest appointment resource searchParam 2 is valid

	CHECK xpathmatches $Appointment/fhir:searchParam[3]/fhir:name/@value ^identifier|appointmentId|version$
	ANNOTATION Get metadata rest appointment resource searchParam 3 is valid

	# Slot Rest Resource
	CHECK xpathmatches count(/fhir:CapabilityStatement/fhir:rest/fhir:resource[fhir:type/@value='Slot']/fhir:interaction/fhir:code)  ^1$
	ANNOTATION Get metadata rest slot resource interaction count is valid

	SET literal $Slot /fhir:CapabilityStatement/fhir:rest/fhir:resource[fhir:type/@value='Slot']

	CHECK xpathexists  $Slot/fhir:interaction/fhir:code[@value='search-type']
	ANNOTATION Get metadata rest slot resource interaction is valid

	CHECK xpathmatches count(/fhir:CapabilityStatement/fhir:rest/fhir:resource[fhir:type/@value='Slot']/fhir:searchParam)  ^4$
	ANNOTATION Get metadata rest slot resource searchParam count is valid

	CHECK xpathmatches count(/fhir:CapabilityStatement/fhir:rest/fhir:resource[fhir:type/@value='Slot']/fhir:searchParam/fhir:name/@value)=count(distinct-values(/fhir:CapabilityStatement/fhir:rest/fhir:resource[fhir:type/@value='Slot']/fhir:searchParam/fhir:name/@value))  ^true$
	ANNOTATION Get metadata rest slot resource searchParams are unique

	CHECK xpathmatches $Slot/fhir:searchParam[1]/fhir:name/@value ^service|status|start|end$
	ANNOTATION Get metadata rest slot resource searchParam 1 is valid

	CHECK xpathmatches $Slot/fhir:searchParam[2]/fhir:name/@value ^service|status|start|end$
	ANNOTATION Get metadata rest slot resource searchParam 2 is valid

	CHECK xpathmatches $Slot/fhir:searchParam[3]/fhir:name/@value ^service|status|start|end$
	ANNOTATION Get metadata rest slot resource searchParam 3 is valid

	CHECK xpathmatches $Slot/fhir:searchParam[4]/fhir:name/@value ^service|status|start|end$
	ANNOTATION Get metadata rest slot resource searchParam 4 is valid

	CHECK xpathmatches count(/fhir:CapabilityStatement/fhir:rest/fhir:resource[fhir:type/@value='Slot']/fhir:searchInclude)  ^6$
	ANNOTATION Get metadata rest slot resource searchInclude count is valid

	CHECK xpathmatches count(/fhir:CapabilityStatement/fhir:rest/fhir:resource[fhir:type/@value='Slot']/fhir:searchInclude/@value)=count(distinct-values(/fhir:CapabilityStatement/fhir:rest/fhir:resource[fhir:type/@value='Slot']/fhir:searchInclude/@value))  ^true$
	ANNOTATION Get metadata rest slot resource searchParams are unique

	CHECK xpathmatches $Slot/fhir:searchInclude[1]/@value ^Slot.schedule|Schedule:actor:Practitioner|Schedule:actor:PractitionerRole|Schedule:actor:HealthcareService|HealthcareService.providedBy|HealthcareService.location$
	ANNOTATION Get metadata rest slot resource searchInclude 1 is valid

	CHECK xpathmatches $Slot/fhir:searchInclude[2]/@value ^Slot.schedule|Schedule:actor:Practitioner|Schedule:actor:PractitionerRole|Schedule:actor:HealthcareService|HealthcareService.providedBy|HealthcareService.location$
	ANNOTATION Get metadata rest slot resource searchInclude 2 is valid

	CHECK xpathmatches $Slot/fhir:searchInclude[3]/@value ^Slot.schedule|Schedule:actor:Practitioner|Schedule:actor:PractitionerRole|Schedule:actor:HealthcareService|HealthcareService.providedBy|HealthcareService.location$
	ANNOTATION Get metadata rest slot resource searchInclude 3 is valid

	CHECK xpathmatches $Slot/fhir:searchInclude[4]/@value ^Slot.schedule|Schedule:actor:Practitioner|Schedule:actor:PractitionerRole|Schedule:actor:HealthcareService|HealthcareService.providedBy|HealthcareService.location$
	ANNOTATION Get metadata rest slot resource searchInclude 4 is valid

	CHECK xpathmatches $Slot/fhir:searchInclude[5]/@value ^Slot.schedule|Schedule:actor:Practitioner|Schedule:actor:PractitionerRole|Schedule:actor:HealthcareService|HealthcareService.providedBy|HealthcareService.location$
	ANNOTATION Get metadata rest slot resource searchInclude 5 is valid

	CHECK xpathmatches $Slot/fhir:searchInclude[6]/@value ^Slot.schedule|Schedule:actor:Practitioner|Schedule:actor:PractitionerRole|Schedule:actor:HealthcareService|HealthcareService.providedBy|HealthcareService.location$
	ANNOTATION Get metadata rest slot resource searchInclude 6 is valid

#==============================================================================
