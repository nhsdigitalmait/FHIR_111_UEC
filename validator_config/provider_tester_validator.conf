# emergency booking response validator ruleset used by tkw autotest
# NHS Interoperability Toolkit
#
#
#
# *****************************************************************************************

VALIDATION-RULESET-NAME FHIR REST Care Connect Emergency booking validations
VALIDATION-RULESET-VERSION 0.1
VALIDATION-RULESET-TIMESTAMP 20201016
VALIDATION-RULESET-AUTHOR Simon Farrow

#==========================================================================================

VALIDATE urn:nhs:names:services:careconnect:fhir:rest:read:metadata
#(remember to use 'VALIDATE-AS: urn:nhs:names:services:careconnect:fhir:rest:read:metadata with the messages)
# Now for DSTU3

CHECK hapifhirvalidator
## response success 
#Schema Check on whole message
#CHECK schema TKW_ROOT/contrib/DMS_Schema/FGM/Schemas/bundle.xsd
#ANNOTATION Schema check (incorrect schema!)

#Transform Checks
CHECK xslt TKW_ROOT/contrib/Common/xslt/blank_attribute_checker.xslt ERROR
ANNOTATION "WARNING ONLY - test shows as fail if XML contains blank attributes"

CHECK xslt TKW_ROOT/contrib/Common/xslt/IllegalCharacters.xslt ERROR
ANNOTATION "WS-STD-01: Toolkit Messages MUST use UTF-8 encoding. Illegal character check may flag up some valid unicode characters, as it is not an exhaustive check. It will locate the use of non-rendering unicode control characters within the payload, sometimes erroneously introduced."

IF xpathexists /fhir:CapabilityStatement
THEN
	CHECK sub capability
END IF

#==========================================================================================
#

VALIDATE urn:nhs:names:services:careconnect:fhir:rest:search:slot
#(remember to use 'VALIDATE-AS: urn:nhs:names:services:gpconnect:fhir:rest:search:slot with the messages)

CHECK hapifhirvalidator

IF xpathexists /fhir:Bundle
THEN
	CHECK sub slots
ELSE
	CHECK content xpathexists /fhir:OperationOutcome
	ANNOTATION Search slots fail check
END IF

#==========================================================================================
# Appointment Retrieve

VALIDATE urn:nhs:names:services:careconnect:fhir:rest:read:appointment

CHECK hapifhirvalidator

IF xpathexists /fhir:Appointment
THEN
	CHECK sub appointment
ELSE
	CHECK content xpathexists /fhir:OperationOutcome
	ANNOTATION Retrieve appointment fail check
END IF

#==============================================================================
# Appointment Book

VALIDATE urn:nhs:names:services:careconnect:fhir:rest:create:appointment

CHECK hapifhirvalidator

IF xpathexists /fhir:Appointment
THEN
	CHECK sub appointment
ELSE
	CHECK content xpathexists /fhir:OperationOutcome
	ANNOTATION Retrieve appointment fail check
END IF

#==============================================================================
# Appointment Cancel

VALIDATE urn:nhs:names:services:careconnect:fhir:rest:update:appointment

CHECK hapifhirvalidator

IF xpathexists /fhir:Appointment
THEN
	CHECK sub appointment
ELSE
	CHECK content xpathexists /fhir:OperationOutcome
	ANNOTATION Cancel appointment fail check
END IF

#==============================================================================
#
SUBSET slots

	CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Slot
	ANNOTATION Get slots success check

	# schedules unique
	CHECK xpathequals boolean(count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Schedule) = count(distinct-values(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Schedule/fhir:id/@value))) true
	ANNOTATION All schedule ids must be unique (SCAL Requirement P12)
	
	# health care services unique
	CHECK xpathequals boolean(count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:HealthCareService) = count(distinct-values(/fhir:Bundle/fhir:entry/fhir:resource/fhir:HealthCareService/fhir:id/@value))) true
	ANNOTATION All HealthCareService ids must be unique (SCAL Requirement P12)
	
	# practitioners unique
	CHECK xpathequals boolean(count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Practitioner) = count(distinct-values(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Practitioner/fhir:id/@value))) true
	ANNOTATION All Practitioner ids must be unique (SCAL Requirement P12)
	
	# practitioner roles unique
	CHECK xpathequals boolean(count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:PractitionerRole) = count(distinct-values(/fhir:Bundle/fhir:entry/fhir:resource/fhir:PractitionerRole/fhir:id/@value))) true
	ANNOTATION All PractitionerRole ids must be unique (SCAL Requirement P12)

	# locations unique
	CHECK xpathequals boolean(count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Location) = count(distinct-values(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Location/fhir:id/@value))) true
	ANNOTATION All Location ids must be unique (SCAL Requirement P12)

	# slots profile check
	CHECK xpathequals boolean(count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Slot/fhir:meta/fhir:profile/@value['https://fhir.hl7.org.uk/STU3/StructureDefinition/CareConnect-Slot-1']) = count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Slot)) true
	ANNOTATION All slot records should have a profile value of https://fhir.hl7.org.uk/STU3/StructureDefinition/CareConnect-Slot-1 (SCAL Requirement P14))
	
	# slots schedule actor
	CHECK xpathequals boolean(count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Schedule/fhir:actor/fhir:reference/@value[starts-with(.,'HealthcareService/')]) = count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Schedule)) true
	ANNOTATION Each schedule response in the response must have an actor referencing HealthcareService (SCAL Requirement P13)
	
	# slots valid schedule reference
	CHECK xpathequals boolean(count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Slot/fhir:schedule/fhir:reference/@value[not(starts-with(.,'Schedule/'))]) = 0) true
	ANNOTATION All slot records should have valid schedule references valid (SCAL Requirement P15)

	# slots start time exists
	CHECK xpathequals boolean(count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Slot/fhir:start/@value) = count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Slot)) true
	ANNOTATION All slot records should have a start timestamp (SCAL Requirement P16)
	
	# slots end time exists
	CHECK xpathequals boolean(count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Slot/fhir:end/@value) = count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Slot)) true
	ANNOTATION All slot records should have an end timestamp (SCAL Requirement P17)

	# slot end dates must be later than start dates
	CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Slot/fhir:start/@value[ . > ../../fhir:end/@value]) 0
	ANNOTATION No slot should have an end date preceeding the start date

#==============================================================================
#

SUBSET appointment
	CHECK xpathexists /fhir:Appointment
	ANNOTATION Get appointment success check

	# C39 appointment profile check - handled by profiles?
	CHECK xpathequals /fhir:Appointment/fhir:meta/fhir:profile/@value https://fhir.hl7.org.uk/STU3/StructureDefinition/CareConnect-Appointment-1
	ANNOTATION All Appointment records should have a profile value of https://fhir.hl7.org.uk/STU3/StructureDefinition/CareConnect-Appointment-1 (SCAL Requirement C39)
	
	# C41 appointment resource declared language must be en-GB
	# DN Is this from the docref? Can't see any other languages
	CHECK xpathequals //fhir:DocumentReference/fhir:content/fhir:attachment/fhir:language/@value en-GB
	ANNOTATION All Appointment records should have a declared language of en-GB (SCAL Requirement C41)
	
	# C43 Appointment references a single slot
	CHECK xpathequals count(/fhir:Appointment/fhir:slot/fhir:reference) 1
	ANNOTATION All Appointment records should reference a single slot (SCAL Requirement C43)

	# C44 Appointment slot reference is correctly formed
	CHECK xpathmatches /fhir:Appointment/fhir:slot/fhir:reference/@value ^.*/Slot/.*$
	ANNOTATION All Appointment slot references should have the correct form (SCAL Requirement C44)

	# C45 Appointment created date must be of the form yyyy-mm-ddThh:MM:SS.sssZ SCAL C45
	CHECK xpathmatches /fhir:Appointment/fhir:created/@value ^[0-9]{4}(-[0-9]{2}){2}T([0-9]{2}:){2}[0-9]{2}(\.[0-9]+)?Z$
	ANNOTATION All Appointment created dates should have the correct form (SCAL Requirement C45)

#==============================================================================
#

SUBSET capability
	CHECK xpathexists /fhir:CapabilityStatement/fhir:publisher
	ANNOTATION Get metadata success check
#
#==============================================================================
