# NEMS validations ruleset
# NHS Interoperability Toolkit
#
# v1.0  October 2020		Richard Robinson
# FHIR 111 UEC Booking STU3 | FHIR NHS Booking | 2.0.0-beta |
# *****************************************************************************************

VALIDATION-RULESET-NAME FHIR 111 UEC Booking Configuration Ruleset 
VALIDATION-RULESET-VERSION 0.1
VALIDATION-RULESET-TIMESTAMP October 2020
VALIDATION-RULESET-AUTHOR NHS Digital

#(remember to use 'VALIDATE-AS: SIMULATOR_LOG_REQUEST plus "/Ssp-InteractionID" in the messages to be validated)

#Validate Create Appointments
VALIDATE urn:nhs:names:services:careconnect:fhir:rest:create:appointment
CHECK hapifhirvalidator
ANNOTATION "HAPI FHIR Validation"
CHECK sub validate_context_path
CHECK sub validate_Request_Header
CHECK sub validate_Request_Header_for_messages_with_payload
CHECK sub validate_JWT
CHECK sub create_appointment_payload
CHECK sub all_appointment_payload

#Validate Update Appointments
VALIDATE urn:nhs:names:services:careconnect:fhir:rest:update:appointment
CHECK hapifhirvalidator
ANNOTATION "HAPI FHIR Validation"
CHECK sub validate_context_path
CHECK sub validate_Request_Header
CHECK sub validate_Request_Header_for_messages_with_payload
CHECK sub validate_JWT
CHECK sub validate_Update_Header
CHECK sub cancel_appointment_payload
CHECK sub all_appointment_payload

#Validate Search Slots
VALIDATE urn:nhs:names:services:careconnect:fhir:rest:search:slot
#ANNOTATION "SEARCH SLOTS TEST ANNOTATION"
CHECK sub validate_context_path
CHECK sub validate_http_parameters
CHECK sub validate_Request_Header
CHECK sub validate_JWT

#Validate retreive an Appointment
VALIDATE urn:nhs:names:services:careconnect:fhir:rest:read:appointment
#ANNOTATION "RETRIEVE AND APPOINTMENT TEST ANNOTATION"
CHECK sub validate_context_path
CHECK sub validate_Request_Header
CHECK sub validate_JWT

#Validate get Metadata
VALIDATE urn:nhs:names:services:careconnect:fhir:rest:read:metadata
CHECK sub validate_Request_Header_for_messages_with_payload
CHECK sub validate_context_path
CHECK sub validate_Request_Header
CHECK sub validate_JWT

#################################################################
#Check contents of HTTP context path in this sub-routine
#################################################################
SUBSET validate_context_path
# Check that the URLs are concatenated correctly -REMOVED AS IT WILL NEVER APPEAR ON THE CONTEXT PATH
#IF context_path matches ^https:\/\/.+[^\/]\/{1}https://.+$
#THEN
#	CHECK context_path matches ^https:\/\/.+[^\/]\/{1}https://.+$
#	ANNOTATION "URLs must be concatenated correctly - Consumers MUST take care to ensure that one and only one / character is included between the SSP URL and the Provider URL."
#ELSE
#	CHECK context_path matches ^https:\/\/.+[^\/]\/{1}https://.+$
#	ANNOTATION "URLs must be concatenated correctly - Consumers MUST take care to ensure that one and only one / character is included between the SSP URL and the Provider URL."
#ENDIF

#Check the URL is encoded C-ENCOD-URLS - DOESNT FIND " space or %not followed by hex
CHECK context_path matches ^[^<>{}|\\^`]*$
ANNOTATION "URL must be encoded properly and not contain unsafe chars"

#################################################################
#Check contents of http parameters in this sub-routine
#################################################################
SUBSET validate_http_parameters

#Check the start parameters 
#	C-INC-END-STAR-SEAR-CRIT
#	C-INC-BEGIN-STAR-SEAR-CRIT
IF context_path matches ^.*(start=le).*$
THEN
	CHECK context_path matches ^.*(start=le).*$
	ANNOTATION "URL le start parameter is present"
	#If there are 2 start parameters check the other is "ge"
	IF context_path matches ^.*(start=.*){2}.+$
	THEN
		IF context_path matches ^.*(start=ge).*$
		THEN
			CHECK context_path matches ^.*(start=ge).*$
			ANNOTATION "Second start parameter is ge"
			#Check that the start parameters are correctly formed
			IF context_path matches ^.*start=ge(-?([0-9]([0-9]([0-9][1-9]|[1-9]0)|[1-9]00)|[1-9]000)(-(0[1-9]|1[0-2])(-(0[1-9]|[1-2][0-9]|3[0-1])(T([01][0-9]|2[0-3])%3A[0-5][0-9]%3A([0-5][0-9]|60)(\\.[0-9]+)?(Z|(%2B|-)((0[0-9]|1[0-3])%3A[0-5][0-9]|14%3A00)))?)?)?).*$
			THEN
				CHECK context_path matches ^.*start=ge(-?([0-9]([0-9]([0-9][1-9]|[1-9]0)|[1-9]00)|[1-9]000)(-(0[1-9]|1[0-2])(-(0[1-9]|[1-2][0-9]|3[0-1])(T([01][0-9]|2[0-3])%3A[0-5][0-9]%3A([0-5][0-9]|60)(\\.[0-9]+)?(Z|(%2B|-)((0[0-9]|1[0-3])%3A[0-5][0-9]|14%3A00)))?)?)?).*$
				ANNOTATION "URL ge start parameters are correctly formed"
			ELSE
				CHECK context_path matches ^.*start=ge(-?([0-9]([0-9]([0-9][1-9]|[1-9]0)|[1-9]00)|[1-9]000)(-(0[1-9]|1[0-2])(-(0[1-9]|[1-2][0-9]|3[0-1])(T([01][0-9]|2[0-3])%3A[0-5][0-9]%3A([0-5][0-9]|60)(\\.[0-9]+)?(Z|(%2B|-)((0[0-9]|1[0-3])%3A[0-5][0-9]|14%3A00)))?)?)?).*$
				ANNOTATION "URL ge start parameters are incorrectly formed"
			ENDIF
			ELSE
			CHECK context_path matches ^.*(start=ge).*$
			ANNOTATION "Second start parameter must be ge"
		ENDIF
	ENDIF
	#Check that the start parameters are correctly formed
	IF context_path matches ^.*start=le(-?([0-9]([0-9]([0-9][1-9]|[1-9]0)|[1-9]00)|[1-9]000)(-(0[1-9]|1[0-2])(-(0[1-9]|[1-2][0-9]|3[0-1])(T([01][0-9]|2[0-3])%3A[0-5][0-9]%3A([0-5][0-9]|60)(\\.[0-9]+)?(Z|(%2B|-)((0[0-9]|1[0-3])%3A[0-5][0-9]|14%3A00)))?)?)?).*$
	THEN
		CHECK context_path matches ^.*start=le(-?([0-9]([0-9]([0-9][1-9]|[1-9]0)|[1-9]00)|[1-9]000)(-(0[1-9]|1[0-2])(-(0[1-9]|[1-2][0-9]|3[0-1])(T([01][0-9]|2[0-3])%3A[0-5][0-9]%3A([0-5][0-9]|60)(\\.[0-9]+)?(Z|(%2B|-)((0[0-9]|1[0-3])%3A[0-5][0-9]|14%3A00)))?)?)?).*$
		ANNOTATION "URL le start parameters are correctly formed"
	ELSE
		CHECK context_path matches ^.*start=le(-?([0-9]([0-9]([0-9][1-9]|[1-9]0)|[1-9]00)|[1-9]000)(-(0[1-9]|1[0-2])(-(0[1-9]|[1-2][0-9]|3[0-1])(T([01][0-9]|2[0-3])%3A[0-5][0-9]%3A([0-5][0-9]|60)(\\.[0-9]+)?(Z|(%2B|-)((0[0-9]|1[0-3])%3A[0-5][0-9]|14%3A00)))?)?)?).*$
		ANNOTATION "URL le start parameters are incorrectly formed"
	ENDIF
ELSE
	CHECK context_path matches ^.*(start=le).*$
	ANNOTATION "There should be 1 le URL start parameter"
ENDIF

#Check the healthcareservice id parameter C-INC-HEAL-SER-ID-SEAR-CRIT
CHECK context_path matches ^.*schedule.actor%3Ahealthcareservice=.*$
ANNOTATION "URL must contain a healthcareservice parameter"

#Check the schedule parameter C-INC-SCHEDULE
CHECK context_path matches ^.*_include=Slot%3Aschedule.*$
ANNOTATION "URL must contain a schedule parameter"

#Check the Healthcare service parameter C-INC-HEAL-SERV
CHECK context_path matches ^.*_include=Schedule%3Aactor%3AHealthcareService.*$
ANNOTATION "URL must contain a healthcare service parameter"

#Check the Practitioner parameter C-INC-PRACT
CHECK context_path matches ^.*_include=Schedule%3Aactor%3APractitioner.*$
ANNOTATION "URL must contain a practitioner parameter"

#Check the Practitioner Role parameter C-INC-PRACTROLE
CHECK context_path matches ^.*_include=Schedule%3Aactor%3APractitionerRole.*$
ANNOTATION "URL must contain a practitioner role parameter"


#################################################################
#Check contents of HTTP Request Header for messages with a payload body in this sub-routine
#################################################################
SUBSET validate_Request_Header_for_messages_with_payload

#SET literal $Location VALIDATION_HTTP_OF_HEADERS


IF http_header Accept matches ^application/fhir\+xml$|^application/fhir\+json$|^application/fhir\+xml;charset=utf-8$|^application/fhir\+json;charset=utf-8$
THEN
	CHECK http_header Accept matches ^application/fhir\+xml$|^application/fhir\+json$|^application/fhir\+xml;charset=utf-8$|^application/fhir\+json;charset=utf-8$
	ANNOTATION "Accept http header valid value valid MIME-type"
ELSE
	IF http_header Accept matches ^application/xml\+fhir$|^application/json\+fhir$|^application/xml\+fhir;charset=utf-8$|^application/json\+fhir;charset=utf-8$
	THEN
		CHECK http_header Accept matches ^application/xml\+fhir$|^application/json\+fhir$|^application/xml\+fhir;charset=utf-8$|^application/json\+fhir;charset=utf-8$
		ANNOTATION "WARNING Accept http header valid  MIME-type is currently accepted (for backward compatibility DSTU2)"
	ELSE
		CHECK http_header Accept matches ^application/xml\+fhir$|^application/json\+fhir$|^application/fhir\+xml$|^application/fhir\+json$|^application/xml$|^application/json$|^text/json$
		ANNOTATION "Accept http header invalid value. Not one of application/fhir+xml, application/fhir+json, application/xml+fhir, application/json+fhir, application/fhir+xml;charset=utf-8, application/fhir+json;charset=utf-8, application/xml+fhir;charset=utf-8, application/json+fhir;charset=utf-8"
	ENDIF
ENDIF


#################################################################
#Check contents of HTTP Request Header in this sub-routine
#################################################################
SUBSET validate_Request_Header

#Authorizatiion
CHECK http_header Authorization matches ^.+$
ANNOTATION "Authorization is mandatory and holds the base64url encoded JSON web token required for audit on the spine"

#JWT Validation - MUST include Bearer in the header added 18 Oct 2018 - Chris Brown. 
IF http_header Authorization matches ^.+$
THEN
	CHECK http_header Authorization matches ^\s?Bearer\s+([a-zA-Z0-9\-_]+)\.([a-zA-Z0-9\-_]+)\.$
	ANNOTATION "Authorization - the JWT must be three base64url encoded strings separated by dots. The final section (signature) must be empty."
ENDIF


#Ssp-From - format ASID 
#	C-ADD-SSP-FROM-HEAD
#	C-ADD-SSP-APP-FROM-HEAD
CHECK http_header Ssp-From matches ^[0-9]{12}$
ANNOTATION "Ssp-From httpheader well formed"

#Ssp-To - format ASID
#	C-ADD-SSP-TO-HEAD
#	C-ADD-SSP-APP-TO-HEAD
CHECK http_header Ssp-To matches ^[0-9]{12}$
ANNOTATION "Ssp-To httpheader well formed"

#Ssp-TraceID - format UUID
#	C-ADD-SSP-TRACE-ID-HEAD
#	C-ADD-SSP-APP-TRACE-ID-HEAD
CHECK http_header Ssp-TraceID matches ^(([0-9]|[a-f]){8}-([0-9]|[a-f]){4}-([0-9]|[a-f]){4}-([0-9]|[a-f]){4}-([0-9]|[a-f]){12}).*$
ANNOTATION "Ssp-TraceID httpheader well formed"

#Ssp-InteractionID - format Acceptable Interaction ID see spec
#	C-ADD-SSP-INTER-ID-HEAD
#	C-ADD-SSP-APP-INTER-ID-HEAD
IF http_header Ssp-InteractionID equals urn:nhs:names:services:careconnect:fhir:rest:search:slot
THEN
	CHECK http_header Ssp-InteractionID equals urn:nhs:names:services:careconnect:fhir:rest:search:slot
	ANNOTATION "Ssp-InteractionID valid value urn:nhs:names:services:careconnect:fhir:rest:search:slot"
ELSE
	IF http_header Ssp-InteractionID equals urn:nhs:names:services:careconnect:fhir:rest:create:appointment
	THEN
		CHECK http_header Ssp-InteractionID equals urn:nhs:names:services:careconnect:fhir:rest:create:appointment
		ANNOTATION "Ssp-InteractionID valid value urn:nhs:names:services:careconnect:fhir:rest:create:appointment"
	ELSE
		IF http_header Ssp-InteractionID equals urn:nhs:names:services:careconnect:fhir:rest:update:appointment
		THEN
			CHECK http_header Ssp-InteractionID equals urn:nhs:names:services:careconnect:fhir:rest:update:appointment
			ANNOTATION "Ssp-InteractionID valid value urn:nhs:names:services:careconnect:fhir:rest:update:appointment"
		ELSE
			IF http_header Ssp-InteractionID equals urn:nhs:names:services:careconnect:fhir:rest:read:metadata
			THEN
				CHECK http_header Ssp-InteractionID equals urn:nhs:names:services:careconnect:fhir:rest:read:metadata
				ANNOTATION "Ssp-InteractionID valid value urn:nhs:names:services:careconnect:fhir:rest:read:metadata"
			ELSE
				IF http_header Ssp-InteractionID equals urn:nhs:names:services:careconnect:fhir:rest:read:appointment
				THEN
					CHECK http_header Ssp-InteractionID equals urn:nhs:names:services:careconnect:fhir:rest:read:appointment
					ANNOTATION "Ssp-InteractionID valid value urn:nhs:names:services:careconnect:fhir:rest:read:appointment"
				ELSE
					CHECK http_header Ssp-InteractionID equals valid_Ssp-InteractionID
					ANNOTATION "Ssp-InteractionID not valid value of urn:nhs:names:services:careconnect:fhir:rest:search:slot, urn:nhs:names:services:careconnect:fhir:rest:create:appointment or urn:nhs:names:services:careconnect:fhir:rest:update:appointment, urn:nhs:names:services:careconnect:fhir:rest:read:metadata, urn:nhs:names:services:careconnect:fhir:rest:read:appointment"
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF

#################################################################
#Check contents of Appointment in this sub-routine
#################################################################
SUBSET all_appointment_payload
#Check that the patient identifier field is only used for NHS number
#	C-POST-INC-APP-RES-NHS-NO-CONDT-PATIENT-RES-ONE-IDENT
#	C-POST-INC-APP-RES-NOT-NHS-NO-CONDT-PATIENT-RES-NOT-IDENT

IF xpathequals count(/fhir:Appointment/fhir:contained/fhir:Patient/fhir:identifier) 1
THEN
	CHECK xpathequals /fhir:Appointment/fhir:contained/fhir:Patient/fhir:identifier/fhir:system/@value https://fhir.nhs.uk/Id/nhs-number
	ANNOTATION "Patients with an identifier must have an NHS Number"
#	C-ACT-RES-CON-SYS-NHS-NO-ACT-RES-ONE-IDENT-USE
	CHECK xpathequals /fhir:Appointment/fhir:participant/fhir:actor/fhir:identifier/fhir:use/@value official
	ANNOTATION "Patients with an NHS Number must have a Participant actor use:official - C-ACT-RES-CON-SYS-NHS-NO-ACT-RES-ONE-IDENT-USE"
	CHECK xpathequals /fhir:Appointment/fhir:participant/fhir:actor/fhir:identifier/fhir:system/@value https://fhir.nhs.uk/Id/nhs-number
	ANNOTATION "Patients with an NHS Number must have a Participant actor correct NHS Number system - C-ACT-RES-CON-SYS-NHS-NO-ACT-RES-ONE-IDENT-USE"
	CHECK xpathmatches /fhir:Appointment/fhir:participant/fhir:actor/fhir:identifier/fhir:value/@value ^[0-9]{10}$
	ANNOTATION "Patients with an NHS Number must have a Participant actor NHS Number - C-ACT-RES-CON-SYS-NHS-NO-ACT-RES-ONE-IDENT-USE"
#	C-ACT-RES-DPLAY-VALUE-FULL-NAME-READ-FORMAT
	CHECK xpathmatches /fhir:Appointment/fhir:participant/fhir:actor/fhir:display/@value ^.+$
	ANNOTATION "Participant actor must be human readable - C-ACT-RES-DPLAY-VALUE-FULL-NAME-READ-FORMAT"
#	C-PATIENT-RES-CONS-SYS-NHS-NO-ONE-IDENT-OFFICIAL
	CHECK xpathequals /fhir:Appointment/fhir:contained/fhir:Patient/fhir:identifier/fhir:use/@value official
	ANNOTATION "Patients with an NHS Number must have a Patient resource use:official - C-PATIENT-RES-CONS-SYS-NHS-NO-ONE-IDENT-OFFICIAL"
	CHECK xpathequals /fhir:Appointment/fhir:contained/fhir:Patient/fhir:identifier/fhir:system/@value https://fhir.nhs.uk/Id/nhs-number
	ANNOTATION "Patients with an NHS Number must have a Patient resource correct NHS Number system - C-PATIENT-RES-CONS-SYS-NHS-NO-ONE-IDENT-OFFICIAL"
	CHECK xpathmatches /fhir:Appointment/fhir:contained/fhir:Patient/fhir:identifier/fhir:value/@value ^[0-9]{10}$
	ANNOTATION "Patients with an NHS Number must have a Patient resource NHS Number - C-PATIENT-RES-CONS-SYS-NHS-NO-ONE-IDENT-OFFICIAL"
#	C-PATIENT-RES-CONS-SYS-NHS-NO-INC-EXT
	CHECK xpathequals /fhir:Appointment/fhir:contained/fhir:Patient/fhir:identifier/fhir:extension/@url https://fhir.hl7.org.uk/STU3/StructureDefinition/Extension-CareConnect-NHSNumberVerificationStatus-1
	ANNOTATION "The contained Patient resource must include the extension https://fhir.hl7.org.uk/STU3/StructureDefinition/Extension-CareConnect-NHSNumberVerificationStatus-1 - C-PATIENT-RES-CONS-SYS-NHS-NO-INC-EXT"
ELSE
	CHECK xpathnotequals /fhir:Appointment/fhir:contained/fhir:Patient/fhir:identifier/fhir:system/@value https://fhir.nhs.uk/Id/nhs-number
	ANNOTATION "Patients without an identifier must not have an NHS Number"
ENDIF
#	C-PATIENT-RES-NAME-OFFI-FAMI-GIVE-NAME-NOT-NULL
CHECK xpathequals /fhir:Appointment/fhir:contained/fhir:Patient/fhir:name/fhir:use/@value official
ANNOTATION "Patients must have a name use:official - C-PATIENT-RES-NAME-OFFI-FAMI-GIVE-NAME-NOT-NULL"
CHECK xpathequals count(/fhir:Appointment/fhir:contained/fhir:Patient/fhir:name/fhir:family)=1 true
ANNOTATION "Patients must have one occurance of name family - C-PATIENT-RES-NAME-OFFI-FAMI-GIVE-NAME-NOT-NULL"
CHECK xpathequals count(/fhir:Appointment/fhir:contained/fhir:Patient/fhir:name/fhir:given)>0 true
ANNOTATION "Patients must have one or more occurances of name given - C-PATIENT-RES-NAME-OFFI-FAMI-GIVE-NAME-NOT-NULL"
#	C-PATIENT-RES-CONT-POINT-SYS-PHONE-VALID-UK-NO
CHECK xpathequals /fhir:Appointment/fhir:contained/fhir:Patient/fhir:telecom/fhir:system/@value phone
ANNOTATION "Patients must have a telecom system:phone - C-PATIENT-RES-CONT-POINT-SYS-PHONE-VALID-UK-NO"
CHECK xpathmatches /fhir:Appointment/fhir:contained/fhir:Patient/fhir:telecom/fhir:value/@value ^.+$
ANNOTATION "Patients must have a telecom system:phone - C-PATIENT-RES-CONT-POINT-SYS-PHONE-VALID-UK-NO"
#	C-PATIENT-RES-GENDER-VALUE
CHECK xpathmatches /fhir:Appointment/fhir:contained/fhir:Patient/fhir:gender/@value ^.+$
ANNOTATION "Patients must have a gender value - C-PATIENT-RES-GENDER-VALUE"
#	C-PATIENT-RES-DOB-VALUE
CHECK xpathmatches /fhir:Appointment/fhir:contained/fhir:Patient/fhir:birthDate/@value ^.+$
ANNOTATION "Patients must have a birth date value - C-PATIENT-RES-DOB-VALUE"
#	C-PATIENT-RES-ONE-ADDRESS
CHECK xpathequals count(/fhir:Appointment/fhir:contained/fhir:Patient/fhir:address)=1 true
ANNOTATION "Patients must have one address set- C-PATIENT-RES-ONE-ADDRESS"
#	C-PATIENT-RES-ONE-ADDRESS-SET-HOME
CHECK xpathequals /fhir:Appointment/fhir:contained/fhir:Patient/fhir:address/fhir:use/@value	home
ANNOTATION "Patients must have one address set to home- C-PATIENT-RES-ONE-ADDRESS-SET-HOME"
#	C-PATIENT-RES-HOME-ADDRESS-VALID-PCODE
CHECK xpathmatches /fhir:Appointment/fhir:contained/fhir:Patient/fhir:address[fhir:use/@value="home"]/fhir:postalCode/@value	^.+$
ANNOTATION "Patient's home address must have one postcode- C-PATIENT-RES-HOME-ADDRESS-VALID-PCODE"
#	C-DOCREF-RES-CDA-APP-ONE-DOCREF-RES
CHECK xpathequals count(/fhir:Appointment/fhir:contained/fhir:DocumentReference)=1 true
ANNOTATION "The Appointment has exactly one contained DocumentReference resource. - C-DOCREF-RES-CDA-APP-ONE-DOCREF-RES"
#	C-DOCREF-RES-IDENT-SYS-VALUE-ROOT-ID-CDA-DOC
CHECK xpathmatches /fhir:Appointment/fhir:contained/fhir:DocumentReference/fhir:identifier/fhir:value/@value ([0-9]|[a-f]){8}-([0-9]|[a-f]){4}-([0-9]|[a-f]){4}-([0-9]|[a-f]){4}-([0-9]|[a-f]){12}
ANNOTATION "The contained DocumentReference resource must have an identifier of system uuid  - C-DOCREF-RES-IDENT-SYS-VALUE-ROOT-ID-CDA-DOC"
#	C-DOCREF-RES-STAT-CUR
CHECK xpathequals /fhir:Appointment/fhir:contained/fhir:DocumentReference/fhir:status/@value current
ANNOTATION "The contained DocumentReference resource must have a status of "current". - C-DOCREF-RES-STAT-CUR"
#	C-DOCREF-RES-TYPE-SYS-CODE-DPLAY
CHECK xpathequals /fhir:Appointment/fhir:contained/fhir:DocumentReference/fhir:type/fhir:coding/fhir:system/@value urn:oid:2.16.840.1.113883.2.1.3.2.4.18.17
ANNOTATION "The contained DocumentReference resource must have a type of coding with values: system - C-DOCREF-RES-TYPE-SYS-CODE-DPLAY"
CHECK xpathequals /fhir:Appointment/fhir:contained/fhir:DocumentReference/fhir:type/fhir:coding/fhir:code/@value POCD_MT200001GB02
ANNOTATION "The contained DocumentReference resource must have a type of coding with values: code - C-DOCREF-RES-TYPE-SYS-CODE-DPLAY"
CHECK xpathequals /fhir:Appointment/fhir:contained/fhir:DocumentReference/fhir:type/fhir:coding/fhir:display/@value Integrated Urgent Care Report
ANNOTATION "The contained DocumentReference resource must have a type of coding with values: display - C-DOCREF-RES-TYPE-SYS-CODE-DPLAY"
#	C-DOCREF-RES-INDEX-VALUE-CUR-TIME
CHECK xpathmatches /fhir:Appointment/fhir:contained/fhir:DocumentReference/fhir:indexed/@value -?([0-9]([0-9]([0-9][1-9]|[1-9]0)|[1-9]00)|[1-9]000)(-(0[1-9]|1[0-2])(-(0[1-9]|[1-2][0-9]|3[0-1])(T([01][0-9]|2[0-3]):[0-5][0-9]:([0-5][0-9]|60)(\\.[0-9]+)?(Z|(\\+|-)((0[0-9]|1[0-3]):[0-5][0-9]|14:00)))?)?)?
ANNOTATION "The contained DocumentReference resource must have a indexed value of the current time. - C-DOCREF-RES-INDEX-VALUE-CUR-TIME"
#	C-DOCREF-RES-CONTENT-ATTACH-VALUES
CHECK xpathequals /fhir:Appointment/fhir:contained/fhir:DocumentReference/fhir:content/fhir:attachment/fhir:contentType/@value application/hl7-v3+xml
ANNOTATION "The contained DocumentReference resource must have a content of type attachment with values:contentType: application/hl7-v3+xml. - C-DOCREF-RES-CONTENT-ATTACH-VALUES"
CHECK xpathequals /fhir:Appointment/fhir:contained/fhir:DocumentReference/fhir:content/fhir:attachment/fhir:language/@value en
ANNOTATION "The contained DocumentReference resource must have a content of type attachment with values:contentType: language: en - C-DOCREF-RES-CONTENT-ATTACH-VALUES"

#	C-PATIENT-RES-ONE-CONTD
CHECK xpathequals count(/fhir:Appointment/fhir:contained/fhir:Patient/fhir:identifier)<=1 true
ANNOTATION "Patient identifier must not occur more than once - C-PATIENT-RES-ONE-CONTD"
#	C-APP-RES-NOT-ID-VALUE
IF xpathexists /fhir:Appointment/fhir:id
THEN
	CHECK xpathexists /fhir:Appointment/fhir:id
	ANNOTATION "Appointment resource must NOT have an id value. C-APP-RES-NOT-ID-VALUE"
ENDIF
#	C-APP-RES-LANG-ENG-GB
CHECK xpathequals /fhir:Appointment/fhir:language/@value en-GB
ANNOTATION "Appointment resource declares language of en-GB.. C-APP-RES-LANG-ENG-GB"
#	C-APP-RES-REFS-ONE-SLOT
CHECK xpathequals count(/fhir:Appointment/fhir:contained/fhir:Slot)=1 true
ANNOTATION "Appointment resource references exactly one Slot. C-APP-RES-REFS-ONE-SLOT"
	
#	Appointment	start 1..1
CHECK xpathmatches /fhir:Appointment/fhir:start/@value ^.+$
ANNOTATION "Appointment start must be present"
#	Appointment	end 1..1
CHECK xpathmatches /fhir:Appointment/fhir:end/@value ^.+$
ANNOTATION "Appointment end must be present"
#	Appointment	created 1..1
CHECK xpathmatches /fhir:Appointment/fhir:created/@value ^.+$
ANNOTATION "Appointment created must be present"
#	Appointment	description 1..1
CHECK xpathmatches /fhir:Appointment/fhir:description/@value ^.+$
ANNOTATION "Appointment description must be present"
#	Appointment	slot 1..1
CHECK xpathexists /fhir:Appointment/fhir:slot
ANNOTATION "Appointment slot must be present"
#	Appointment	supportingInformation 1..1
CHECK xpathexists /fhir:Appointment/fhir:supportingInformation
ANNOTATION "Appointment supportingInformation must be present"
#	C-APP-RES-APP-ONE-ACT-RES-REF-CONTD-RES - the # reference to the patient is checked with the fhir profile
CHECK xpathequals count(/fhir:Appointment/fhir:participant/fhir:actor)=1 true
ANNOTATION "Appointment has exactly 1 participant actor. C-APP-RES-APP-ONE-ACT-RES-REF-CONTD-RES"
	
#	C-APP-RES-CONTD-SLOT-RES-CLAIM-META
CHECK xpathequals	/fhir:Appointment/fhir:contained/fhir:Slot/fhir:meta/fhir:profile/@value https://fhir.hl7.org.uk/STU3/StructureDefinition/CareConnect-Slot-1
ANNOTATION "The contained Slot resource must claim in Meta section to conform to https://fhir.hl7.org.uk/STU3/StructureDefinition/CareConnect-Slot-1. - C-APP-RES-CONTD-SLOT-RES-CLAIM-META"

#	C-REFAL-REF-CONS-CDA-DOC-APP-ONE-SUPP-INFO-REF-CONTD-RES
IF xpathequals count(/fhir:Appointment/fhir:contained/fhir:DocumentReference)=1 true
THEN
	CHECK xpathnotmatches /fhir:Appointment/fhir:contained/fhir:DocumentReference/fhir:id/@value ^http.+$
	ANNOTATION "Appointment CDA reference must not be external - C-REFAL-REF-CONS-CDA-DOC-APP-ONE-SUPP-INFO-REF-CONTD-RES"
ENDIF

#	C-PATIENT-RES-CLAIM-META-SECT
CHECK xpathequals	/fhir:Appointment/fhir:contained/fhir:Patient/fhir:meta/fhir:profile/@value https://fhir.hl7.org.uk/STU3/StructureDefinition/CareConnect-Patient-1
ANNOTATION "The contained Patient resource must claim in Meta section to conform to https://fhir.hl7.org.uk/STU3/StructureDefinition/CareConnect-Patient-1 - C-PATIENT-RES-CLAIM-META-SECT"



#################################################################
#Check contents of Create Appointment in this sub-routine
#################################################################
SUBSET cancel_appointment_payload
#	
IF xpathequals /fhir:Appointment/fhir:status/@value cancelled
THEN
	CHECK xpathequals /fhir:Appointment/fhir:status/@value cancelled
	ANNOTATION "Appointment resource has a status of cancelled."
ENDIF
#################################################################
#Check contents of Create Appointment in this sub-routine
#################################################################
SUBSET create_appointment_payload

#	C-APP-RES-STAT-BOOKED
CHECK xpathequals /fhir:Appointment/fhir:status/@value booked
ANNOTATION "Appointment resource has a status of booked. C-APP-RES-STAT-BOOKED"
#################################################################
#Check contents of HTTP Update Header in this sub-routine
#################################################################
SUBSET validate_Update_Header

IF http_header If-Match matches ^W\/\"[a-z,0-9,-]+"$
THEN
	CHECK http_header If-Match matches ^W\/\"[a-z,0-9,-]+"$
	ANNOTATION "Http header If-Match is present and contains a weak eTag"
ELSE
	CHECK http_header If-Match matches ^W\/\"[a-z,0-9,-]+"$
	ANNOTATION "Http header If-Match is not present or requires a weak eTag"
ENDIF


#################################################################
#Check contents of Common Request JWT block in this sub-routine
#################################################################
SUBSET validate_JWT
#######################################The JWT header doesnt seem to be validated

#ONLY validate contents of the JWT if the JWT itself is valid. Hence the "IF" .....
IF http_header Authorization matches ^\s?Bearer\s+([a-zA-Z0-9\-_]+)\.([a-zA-Z0-9\-_]+)\.$
THEN
	SET literal $Location VALIDATION_OF_JWT

#	iss Issuer
	CHECK jwt_payload xpathexists /json:json/@iss
	ANNOTATION "JWT issuer URI (iss) element is required"
#	sub Subject
	CHECK jwt_payload xpathexists /json:json/@sub
	ANNOTATION "JWT submitter URI (sub) element is required"
#	aud Audience
	CHECK jwt_payload xpathexists /json:json/@aud
	ANNOTATION "JWT requested resource URI (aud) element is required"
#	exp Expiry
	CHECK jwt_payload xpathmatches /json:json/@exp ^[0-9]{10}$
	ANNOTATION "JWT expiry time (exp) element is required and must be valid"
#	iat Issued at
	CHECK jwt_payload xpathmatches /json:json/@iat ^[0-9]{10}$
	ANNOTATION "JWT creation time (iat) element is required and must be valid"

	#JWT EXPIRE set correctly if CREATION and EXPIRE are present and valid.
	IF jwt_payload xpathmatches /json:json/@exp ^[0-9]{10}$
	THEN
		IF jwt_payload xpathmatches /json:json/@iat ^[0-9]{10}$
		THEN

			# JWT EXPIRE Earlier Than CREATION
			CHECK jwt_payload xpathequals boolean(/json:json/@exp < /json:json/json:iat) false
			ANNOTATION "JWT creation time (iat) must be before JWT expiry date (exp)"
		
			CHECK jwt_payload xpathequals boolean(number(/json:json/@exp) - number(/json:json/@iat) = 300) true
			ANNOTATION "JWT creation time (iat) must be 300 seconds earlier than JWT expiry date (exp)"

			IF jwt_payload xpathequals boolean(number(/json:json/@exp) - number(/json:json/@iat) = 300) false
			THEN
				#JWT EXPIRE too soon.
				CHECK jwt_payload xpathequals boolean(number(/json:json/@exp) - number(/json:json/@iat) < 300) false
				ANNOTATION "JWT creation time (iat) must be 300 seconds earlier than JWT expiry date (exp)"
				#JWT EXPIRE too late 
				CHECK jwt_payload xpathequals boolean(number(/json:json/@exp) - number(/json:json/@iat) > 300) false
				ANNOTATION "JWT creation time (iat) must be 300 seconds earlier than JWT expiry date (exp)"
			ENDIF
			
		ENDIF
	ENDIF		

#	reason_for_request
	CHECK jwt_payload xpathequals /json:json/@reason_for_request directcare
	ANNOTATION "UEC apointment booking only supports usage for direct care, this value SHALL be set to directcare."

#	requested_scope
	#CHECK jwt_payload xpathin (tokenize(/json:json/@equested_scope, "\s+"))  patient/appointment.write organization/slot.read
	#ANNOTATION "JWT requested_scope must be an expected value or list of values")
	
#	requesting_device

	CHECK jwt_payload xpathmatches /json:json/json:requesting_device/json:identifiers/json:identifier/@system ^.+$
	ANNOTATION "JWT: requesting_device: system contains a consumer-defined system URL "
	CHECK jwt_payload xpathmatches /json:json/json:requesting_device/json:identifiers/json:identifier/@value ^.+$
	ANNOTATION "JWT: requesting_device: value ccontaining the device or system identifier"
	CHECK jwt_payload xpathmatches /json:json/json:requesting_device/@model ^.+$
	ANNOTATION "JWT: requesting_device: model containing consumer product or system name"
	CHECK jwt_payload xpathmatches /json:json/json:requesting_device/@version ^.+$
	ANNOTATION "JWT: requesting_device: containing the version number of the consumer product or system"
	
#	requesting_organisation

	CHECK jwt_payload xpathmatches /json:json/json:requesting_organization/@name ^.+$
	ANNOTATION "JWT: requesting_organization: name containing the name of the organisation"
	CHECK jwt_payload xpathmatches /json:json/json:requesting_organization/json:identifiers/json:identifier/@system ^.+$
	ANNOTATION "JWT: requesting_organization: system containing https://fhir.nhs.uk/Id/ods-organization-code"
	CHECK jwt_payload xpathmatches /json:json/json:requesting_organization/json:identifiers/json:identifier/@value ^.+$
	ANNOTATION "JWT: requesting_organization: value containing the ODS code of the organisation"

#	requesting_practitioner (Could)

	IF jwt_payload xpathexists /json:json/json:requesting_practitioner
	THEN
		CHECK jwt_payload xpathcompare /json:json/@sub /json:json/json:requesting_practitioner/@id
		ANNOTATION "ID for the user on whose behalf this request is being made. Matches requesting_practitioner.id if present"
		CHECK jwt_payload xpathmatches /json:json/json:requesting_practitioner/json:names/json:name/@family ^.+$
		ANNOTATION "JWT: requesting_practitioner: family containing the user’s family name"
		CHECK jwt_payload xpathmatches /json:json/json:requesting_practitioner/json:names/json:name/json:givens/json:given ^.+$
		ANNOTATION "JWT: requesting_practitioner: given containing the user’s given name"
		
		
### NEEDS CHANGING WHEN THE JSON TO XML CODE IS FIXED
		CHECK jwt_payload xpathequals /json:json/json:requesting_practitioner/json:identifiers/json:identifier[@system="https://fhir.nhs.uk/Id/sds-user-id"]/@system https://fhir.nhs.uk/Id/sds-user-id
		ANNOTATION "JWT: requesting_practitioner: system containing https://fhir.nhs.uk/Id/sds-user-id"
		CHECK jwt_payload xpathmatches /json:json/json:requesting_practitioner/json:identifiers/json:identifier[@system="https://fhir.nhs.uk/Id/sds-user-id"]/@value ^[0-9]{12}$|UNK$
		ANNOTATION "JWT: requesting_practitioner: value containing value containing the SDS user ID from the user’s NHS smartcard, or the value UNK if the user is not logged on with an NHS smartcard"
		
		CHECK jwt_payload xpathequals /json:json/json:requesting_practitioner/json:identifiers/json:identifier[@system="https://fhir.nhs.uk/Id/sds-role-profile-id"]/@system https://fhir.nhs.uk/Id/sds-role-profile-id
		ANNOTATION "JWT: requesting_practitioner: system containing https://fhir.nhs.uk/Id/sds-role-profile-id"
		CHECK jwt_payload xpathmatches /json:json/json:requesting_practitioner/json:identifiers/json:identifier[@system="https://fhir.nhs.uk/Id/sds-role-profile-id"]/@value ^[0-9]{12}$|UNK$
		ANNOTATION "JWT: requesting_practitioner: value containing the SDS user ID from the user’s NHS smartcard, or the value UNK if the user is not logged on with an NHS smartcard"
		
		CHECK jwt_payload xpathmatches /json:json/json:requesting_practitioner/json:identifiers/json:identifier[@system!="https://fhir.nhs.uk/Id/sds-user-id"][@system!="https://fhir.nhs.uk/Id/sds-role-profile-id"]/@system ^.+$
		ANNOTATION "JWT: requesting_practitioner: system containing a consumer-defined system URL representing the type of identifier in the value field, e.g. https://consumersupplier.com/Id/user-guid"
		CHECK jwt_payload xpathmatches /json:json/json:requesting_practitioner/json:identifiers/json:identifier[@system!="https://fhir.nhs.uk/Id/sds-user-id"][@system!="https://fhir.nhs.uk/Id/sds-role-profile-id"]/@value ^.+$
		ANNOTATION "JWT: requesting_practitioner: value containing the unique local identifier for the logged on user"
		
	ENDIF

ELSE
	CHECK http_header Authorization matches ^\s?Bearer\s+([a-zA-Z0-9\-_]+)\.([a-zA-Z0-9\-_]+)\.$	
	ANNOTATION "The JWT has not been decoded as the Authorization HTTP header is not valid"
# ENDIF for valid JWT test.....
ENDIF
